apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

static String computeApplicationId(String suffix) {
    String applicationId = 'com.simplaapliko.goldenhour.supporter'
    if (suffix == 'prod') {
        return applicationId
    } else {
        return applicationId + '.' + suffix
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28

        versionCode 5
        versionName "1.0.4"

        vectorDrawables.useSupportLibrary= false
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
        debug {
            minifyEnabled false
            debuggable true
            //applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            ext.alwaysUpdateBuildId = false
            multiDexEnabled true
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "default"

    productFlavors {
        dev {
            applicationId computeApplicationId('dev')
            resConfigs("xxhdpi")
        }
        prod {
            applicationId computeApplicationId('prod')
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.firebase:firebase-core:16.0.3'

    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:mediarouter-v7:28.0.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    implementation 'com.simplaapliko:about:1.0.19'
    implementation 'com.github.simplaapliko:design:0.6.2@aar'
}

apply plugin: 'com.google.gms.google-services'
